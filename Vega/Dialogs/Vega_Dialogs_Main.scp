// Vega: Main Dialog
// Version: Sphere X
// Author: Luxion

[COMMENT Vega_Dialogs]
If you have custom gumps please set your gumps IDs in "Vega\Config\Vega_Config_Design.scp"

// Main dialog structure
[DIALOG d_lux_vega]
100, 100
Ref1 = <Src.FindID.i_mem_vega>

// <open> Setup
local.Width = 700
local.Height = 250
local.WX = 205
local.WY = 49
local.WPX = <Eval (<local.Width>/2)-(<local.WX>/2)>
local.WPY = <Eval 0-(<local.WY>/2)>
// <close> Setup

call VegaInit 720,250
call VegaDiv 10,10,675,20,<Def.Vega_Lang_<Ref1.Tag0.Lang>_Title>,48

call VegaDivEmpty 690,10,20,20
button 692 13 5602 5606 1 0 0

for 0 <Eval <Def0.Vega_Tabs_Total>-1>
	local.X = <Eval (<local._for>*100)+10>
	call VegaDivEmpty <local.X>,35,20,20
	button <eval <local.x>+2> 38 5601 5605 1 0 <Eval 1000+<local._for>>
	if (<Ref1.Tag0.CurTab> == <local._for>)
		VegaDivSub <Eval <local.X>+20>,35,80,20,<Def.Vega_Tabs_<Ref1.Tag0.Lang>_<local._for>_Name>,63
	else
		VegaDivSub <Eval <local.X>+20>,35,80,20,<Def.Vega_Tabs_<Ref1.Tag0.Lang>_<local._for>_Name>
	endif
endfor

CALL Vega_Contents_<Ref1.Tag0.CurTab>

// Tool options
VegaDiv 10,205,700,20,Options
VegaDivEmpty 10,225,20,20
VegaBtnEmpty 10,225,0,200
VegaDivSub 30,225,100,20,Transparent

if (<Src.Ctag0.VegaTransparent>)
	Checkertrans 0 0 720 250
endif

// Button controls

[DIALOG d_lux_vega BUTTON]
ON=100 102
	if (<Argn1> == 101)
		if (<Eval <Src.Light>+5> > 30)
			Src.Light = 30
		else
			Src.Light += 5
		endif
		
	elif (<Argn1> == 100)
		if (<Eval <Src.Light>-5> < 1)
			Src.Light = 1
		else
			Src.Light -= 5
		endif
		
	elif (<Argn1> == 102)
		local.SendLight = <ArgTxt[1]>
		if !(<isnum <local.SendLight>>)
			Src.Sysmessage @435 Must be a number.
		endif
		if (<local.SendLight> > 30)
			Src.Light = 30
		elseif (<local.SendLight> < 1)
			Src.Light = 1
		else
			Src.Light = <local.SendLight>
		endif
	endif
	Src.Vega
	
ON=200
	if (<Src.Ctag0.VegaTransparent>)
		Src.Ctag0.VegaTransparent=
	else
		Src.Ctag0.VegaTransparent=1
	endif
	Src.Vega

ON=1000 1999 //Tab button
	Ref1 = <Src.FindID.i_mem_vega>
	Ref1.Tag0.CurTab = <Eval <Argn1>-1000>
	Src.Vega
	if (<Argn1> == 1003) //Chars
		src.dialogclose d_add
		Src.add
	elif (<Argn1> == 1004) //items
		src.dialogclose d_add
		Src.add
	endif

ON=2000 2999 //Generic button
	Src.Vega
	if (<Argn1> == 2001)
		Src.Admin

	elif (<Argn1> == 2002)
		Src.vInfo
		
	elif (<Argn1> == 2003)
		Src.vEdit
		
	elif (<Argn1> == 2004)
		Src.Sysmessage Current Clients connected: <Eval <Serv.Clients>>
		
	elif (<Argn1> == 2005)
		Src.Dialog d_vega_server
		
	elif (<Argn1> == 2006)
		
	endif

ON=3000 3999 // Misc Button
	Src.Vega
	if (<Argn1> == 3001) //tele
		src.tele
	elif (<Argn1> == 3002) //move

	elif (<Argn1> == 3003) //flip
		src.xflip
	elif (<Argn1> == 3007) //supertele
		src.dialogclose d_lux_vega //If stay open, impossible to cancel supertele
		src.t
	endif

ON=4000 4999 // Weather
	Src.Vega

ON=5000 5999 // GameMaster
	Src.Vega
	if (<Argn1> == 5001) //invis
		if (<src.flags>&statf_insubstantial) //FIX me, QVAL evaluation dont seem to work.... dont know why.
			src.invis=0
		else
			src.invis=1
		endif
	elif (<Argn1> == 5002) //invul
		<QVAL <src.flags>&statf_invul ? src.invul=0 : src.invul=1 >
	elif (<Argn1> == 5003) //Nightsight
		<QVAL <src.nightsight> ? src.nightsight=0 : src.nightsight=1 >
	elif (<Argn1> == 5004) //Summon To ????
		//???
	elif (<Argn1> == 5005) //Page
		src.page
	elif (<Argn1> == 5006) //Jail
		src.xjail
	elif (<Argn1> == 5007) //Forgive
		src.xforgive
	elif (<Argn1> == 5008) //Kill
		src.xkill
	elif (<Argn1> == 5009) //Disconnect
		src.xdisconnect
	elif (<Argn1> == 5010) //Resurrect
		src.xresurrect
	elif (<Argn1> == 5011) //Ban
		src.xban
	elif (<Argn1> == 5012) //Other
		//Open other dialog
	endif

[EOF]